allprojects {
    apply plugin: "java-library"

    ext {
        gdxVersion = '1.10.0'
        graalvmVersion = '21.3.0'
    }

    group 'com.anyicomplex'
    version gdxVersion + '-beta2'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "org.graalvm.nativeimage:svm:$graalvmVersion"
    }

}

dependencies {
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
}

subprojects {
    dependencies {
        implementation project(":")
    }
}

project(":backends:lwjgl3") {
    dependencies {
        // Compile GraalVM platform library
        compileOnly "org.graalvm.nativeimage:svm-hosted-native-linux-amd64:$graalvmVersion"
        compileOnly "org.graalvm.nativeimage:svm-hosted-native-windows-amd64:$graalvmVersion"
        compileOnly "org.graalvm.nativeimage:svm-hosted-native-darwin-amd64:$graalvmVersion"
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    }
}

project(":extensions:bullet") {
    dependencies {
        implementation "com.badlogicgames.gdx:gdx-bullet:$gdxVersion"
    }
}

configure([
        project(":"),
        project(":backends:lwjgl3"),
        project(":backends:moe"),
        project(":extensions:bullet"),

]) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    java {
        withJavadocJar()
        withSourcesJar()
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                def idStr = rootProject.name
                if (project.name != rootProject.name) {
                    idStr += "-"
                    idStr += project.parent.name.substring(0, project.parent.name.length() - 1)
                    idStr += "-"
                    idStr += project.name
                }
                artifactId = idStr
                groupId = project.group
                version = project.version
                from components.java
                pom {
                    def nameStr = "libGDX Graal Helper"
                    if (project.name != rootProject.name) {
                        nameStr += " ("
                        nameStr += project.parent.name.substring(0, 1).toUpperCase()
                        nameStr += project.parent.name.substring(1, project.parent.name.length() - 1)
                        nameStr += " "
                        nameStr += project.name.substring(0, 1).toUpperCase()
                        nameStr += project.name.substring(1, project.name.length())
                        nameStr += ")"
                    }
                    name = nameStr
                    description = 'Experimental helper for libGDX to build GraalVM native-image.'
                    url = 'https://github.com/anyicomplex/gdx-graalhelper'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'anyicomplex'
                            name = 'Yi An'
                            email = 'anyicomplex@protonmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:anyicomplex/gdx-graalhelper.git'
                        developerConnection = 'scm:git:ssh://github.com/anyicomplex/gdx-graalhelper.git'
                        url = 'https://github.com/anyicomplex/gdx-graalhelper'
                    }
                }
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
                def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
            maven {
                name = "OSSRH"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/octocat/hello-world"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

}

