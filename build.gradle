ext {
    isReleaseBuild = {
        return project.hasProperty("RELEASE")
    }
    graalhelperVersion = "2.0.0" + (isReleaseBuild() ? "" : "-SNAPSHOT")
    gdxVersion = '1.12.1'
    gdxControllersVersion = '2.2.3'
    graalvmVersion = '22.3.1'

}

allprojects {
    apply plugin: "java-library"
    group 'io.github.berstanio'

    version graalhelperVersion
}

configure(allprojects - project(":annotations")) {
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        compileOnlyApi "org.graalvm.nativeimage:svm:$graalvmVersion"
    }
}

dependencies {
    api project(":annotations")
    compileOnly "com.badlogicgames.gdx:gdx:$gdxVersion"
}

project(":annotations") {
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }
}

project(":backends:lwjgl3") {
    dependencies {
        api project(":")
        compileOnly "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    }
}

project(":backends:moe") {
    dependencies {
        api project(":")
    }
}

project(":extensions:box2d") {
    dependencies {
        api project(":")
        compileOnly "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    }
}

project(":extensions:bullet") {
    dependencies {
        api project(":")
        compileOnly "com.badlogicgames.gdx:gdx-bullet:$gdxVersion"
    }
}

project(":extensions:freetype") {
    dependencies {
        api project(":")
        compileOnly "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    }
}

project(":extensions:controllers-lwjgl3") {
    dependencies {
        api project(":")
        compileOnly "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
    }
}

project(":extensions:controllers-moe") {
    dependencies {
        api project(":")
    }
}

configure([
        project(":"),
        project(":annotations"),
        project(":backends:lwjgl3"),
        project(":backends:moe"),
        project(":extensions:box2d"),
        project(":extensions:bullet"),
        project(":extensions:freetype"),
        project(":extensions:controllers-lwjgl3"),
        //project(":extensions:controllers-moe"),

]) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withJavadocJar()
        withSourcesJar()
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                def idStr = rootProject.name
                if (project.name != rootProject.name) {
                    idStr += "-"
                    if (project.parent.name != rootProject.name) {
                        idStr += project.parent.name.substring(0, project.parent.name.length() - 1)
                        idStr += "-"
                    }
                    idStr += project.name
                }
                artifactId = idStr
                groupId = project.group
                version = project.version
                from components.java
                pom {
                    def nameStr = "Gdx GraalVM Native Image Helper"
                    if (project.name != rootProject.name) {
                        nameStr += " ("
                        nameStr += project.parent.name.substring(0, 1).toUpperCase()
                        nameStr += project.parent.name.substring(1, project.parent.name.length() - 1)
                        nameStr += " "
                        nameStr += project.name.substring(0, 1).toUpperCase()
                        nameStr += project.name.substring(1, project.name.length())
                        nameStr += ")"
                    }
                    name = nameStr
                    description = 'Experimental helper for libGDX to build GraalVM Native Image.'
                    url = 'https://github.com/anyicomplex/gdx-svmhelper'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'anyicomplex'
                            name = 'Yi An'
                            email = 'anyicomplex@protonmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/anyicomplex/gdx-svmhelper.git'
                        developerConnection = 'scm:git:git@github.com:anyicomplex/gdx-svmhelper.git'
                        url = 'https://github.com/anyicomplex/gdx-svmhelper/tree/main'
                    }
                }
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
                if (!isReleaseBuild()) {
                    url "https://s01.oss.sonatype.org/content/repositories/snapshots"
                } else {
                    url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.mavenJava
    }

    //Simply using "required" in signing block doesn't work because taskGraph isn't ready yet.
    gradle.taskGraph.whenReady {
        tasks.withType(Sign) {
            onlyIf { isReleaseBuild() }
        }
    }
}

