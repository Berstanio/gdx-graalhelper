allprojects {
    apply plugin: "java-library"

    group 'com.anyicomplex'

    ext {
        gdxVersion='1.10.0'
        graalvmVersion='21.3.0'
    }

    version gdxVersion + '-beta2'

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        implementation "org.graalvm.nativeimage:svm:$graalvmVersion"
    }
}

dependencies {
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
}

subprojects {
    dependencies {
        implementation project(":")
    }
}

project(":backends:lwjgl3") {
    dependencies {
        // Compile GraalVM platform library
        compileOnly "org.graalvm.nativeimage:svm-hosted-native-linux-amd64:$graalvmVersion"
        compileOnly "org.graalvm.nativeimage:svm-hosted-native-windows-amd64:$graalvmVersion"
        compileOnly "org.graalvm.nativeimage:svm-hosted-native-darwin-amd64:$graalvmVersion"
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    }
}

project(":extensions:bullet") {
    dependencies {
        implementation "com.badlogicgames.gdx:gdx-bullet:$gdxVersion"
    }
}

configure([
        project(":"),
        project(":backends:lwjgl3"),
        project(":backends:moe"),
        project(":extensions:bullet"),

]) {
    apply plugin: 'maven-publish'
    publishing {
        publications {
            mavenJava(MavenPublication) {
                def idStr = rootProject.name
                if (project.name != rootProject.name) {
                    idStr += "-"
                    idStr += project.parent.name.substring(0, project.parent.name.length() - 1)
                    idStr += "-"
                    idStr += project.name
                }
                artifactId = idStr
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    def nameStr = "libGDX Graal Helper"
                    if (project.name != rootProject.name) {
                        nameStr += " ("
                        nameStr += project.parent.name.substring(0, 1).toUpperCase()
                        nameStr += project.parent.name.substring(1, project.parent.name.length() - 1)
                        nameStr += " "
                        nameStr += project.name.substring(0, 1).toUpperCase()
                        nameStr += project.name.substring(1, project.name.length())
                        nameStr += ")"
                    }
                    name = nameStr
                    description = 'Experimental helper for libGDX to build GraalVM native-image.'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'anyicomplex'
                            name = 'Yi An'
                            email = 'anyicomplex@protonmail.com'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
                def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

